dnl Adapted from libmthca
dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(librxe, 1.0.0)
AC_CONFIG_SRCDIR([src/rxe.c])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(librxe, 1.0.0)
AM_PROG_LIBTOOL

AC_ARG_WITH([valgrind],
    AC_HELP_STRING([--with-valgrind],
        [Enable Valgrind annotations (small runtime overhead, default NO)]))

if test x$with_valgrind = x || test x$with_valgrind = xno; then
    want_valgrind=no
    AC_DEFINE([NVALGRIND], 1, [Define to 1 to disable Valgrind annotations.])
else
    want_valgrind=yes
    if test -d $with_valgrind; then
        CPPFLAGS="$CPPFLAGS -I$with_valgrind/include"
    fi
fi

AC_PROG_CC

AC_ARG_ENABLE([repackage],
    AC_HELP_STRING([--enable-repackage],
        [Enable repackage script]))

if test x$enable_repackage = x || test x$enable_repackage = xno; then
    AC_CHECK_LIB(ibverbs, ibv_get_device_list, [],
        AC_MSG_ERROR([ibv_get_device_list() not found.  librxe requires libibverbs.]))
    AC_CHECK_HEADER(infiniband/driver.h, [],
        AC_MSG_ERROR([<infiniband/driver.h> not found.  librxe requires libibverbs.]))
    AC_CHECK_FUNCS(ibv_read_sysfs_file ibv_dontfork_range ibv_dofork_range \
        ibv_register_driver)
    AC_CHECK_LIB(pthread, pthread_mutex_init, [],
    AC_MSG_ERROR([pthread_mutex_init() not found.  librxe requires libpthread.]))

    dummy=if$$
    cat <<IBV_VERSION > $dummy.c
    #include <infiniband/driver.h>
    IBV_DEVICE_LIBRARY_EXTENSION
IBV_VERSION
    IBV_DEVICE_LIBRARY_EXTENSION=`$CC $CPPFLAGS -E $dummy.c 2> /dev/null | tail -1`
    rm -f $dummy.c
else
    IBV_DEVICE_LIBRARY_EXTENSION="IBV_DEVICE_LIBRARY_EXTENSION"
fi

AM_CONDITIONAL(HAVE_IBV_DEVICE_LIBRARY_EXTENSION,
    test $IBV_DEVICE_LIBRARY_EXTENSION != IBV_DEVICE_LIBRARY_EXTENSION)
AC_SUBST(IBV_DEVICE_LIBRARY_EXTENSION)

AC_HEADER_STDC
AC_CHECK_HEADER(valgrind/memcheck.h,
    [AC_DEFINE(HAVE_VALGRIND_MEMCHECK_H, 1,
        [Define to 1 if you have the <valgrind/memcheck.h> header file.])],
    [if test $want_valgrind = yes; then
        AC_MSG_ERROR([Valgrind memcheck support requested, but <valgrind/memcheck.h> not found.])
    fi])

AC_C_CONST
AC_CHECK_SIZEOF(long)

AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
    [if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
        RXE_VERSION_SCRIPT='-Wl,--version-script=$(srcdir)/src/rxe.map'
    else
        RXE_VERSION_SCRIPT=
    fi])
AC_SUBST(RXE_VERSION_SCRIPT)

AC_CONFIG_FILES([Makefile librxe.spec])
AC_OUTPUT
